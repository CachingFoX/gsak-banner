#*******************************************
# MacVersion = 0.1
# MacDescription = Create banners for your founds
# MacAuthor = CachingFoX
# MacFileName = gen-banner-ui.gsk
# MacUrl = https://raw.githubusercontent.com/CachingFoX/gsak-banner/master/gen-banner-ui.gsk
#*******************************************

$BannerDataUrlUrl = "https://raw.githubusercontent.com/CachingFoX/gsak-banner/master/data/banners.csv"
$BannerListFingerprintUrl = "https://api.github.com/repos/CachingFoX/gsak-banner/contents/data?ref=master"

# -----------------------------------------------
#  Prepare database
#
$status = sqlite("sql",$sqlTableBannerList,"sqlget=0")
$status = sqlite("sql",$sqlTableBannerMetadata,"sqlget=0")

# Prefill form
$rbtnOutputFile = True
MacSettings Type=R FileCheck=N
GOSUB name=CheckForUpdates

While True
    $exit = Form($formMain,"")
    BeginCase
        Case $exit = "SystemExit"
            CANCEL
        Case $exit = "btnClose"
            CANCEL
        Case $exit = "btnDownload"
            GOSUB name=DownloadBannerList            
        Case $exit = "btnGenerate"
            MacSettings Type=S Vars=rbtnOutputFile,rbtnOutputClipboard,editFolderSelect,cboxInterSpaceStation
            GOSUB name=GenerateBanner
    EndCase
EndWhile
CANCEL


BEGINSUB name=CheckForUpdates
    $status = ""
    $enable = "Yes"
    $metadata = GetUrl($BannerListFingerprintUrl)
    IF Equal(Left($metadata,7),"*error*")
        $status="Cannot load meta info for banner list: "+$metadata
    ELSE
        $expression = quote("size")+"\s*:\s*\d*"
        $match = RegExData($expression,$metadata,0)
        $size = RegExData("\d*",$match,0)

        $expression = quote("sha")+"\s*:\s*"+quote("[0-9a-f]+")
        $match = RegExData($expression,$metadata,0)
        $fingerprint = RegExData("[0-9a-f]+",$match,0)

        $_sql = "SELECT value FROM _gen_banner_metadata WHERE key='fingerprint'"
        $current_fingerprint = Sqlite("sql",$_sql)

        IF IsEmpty($current_fingerprint)
            $status = "No local banner list available. Download it."
        ELSE
            IF $current_fingerprint <> $fingerprint
                $status = "A newer version of the banner list is available."
            ELSE
                $status = "Banner list is up-to-date. Have fun!"
                $enable = "No"
            ENDIF
            $date = sqlite("sql","SELECT timestamp FROM _gen_banner_metadata WHERE key='fingerprint'")
            $formMain = EditForm($formMain,"txtDownloadDate","Caption",$date)
            $count = sqlite("sql","SELECT count(*) FROM bannerlist")
            $formMain = EditForm($formMain,"txtNumBanner","Caption",$count)
        ENDIF
    ENDIF
    $formMain = EditForm($formMain,"labelBannerListStatus","Caption",$status)
    $formMain = EditForm($formMain,"btnDownload","Enabled",$enable)
ENDSUB



BEGINSUB name=DownloadBannerList
    $metadata = GetUrl($BannerListFingerprintUrl)
    IF Equal(Left($metadata,7),"*error*")
        MsgOk msg="Cannot load metainfo "+$metadata
        Cancel
    ENDIF

    $expression = quote("size")+"\s*:\s*\d*"
    $match = RegExData($expression,$metadata,0)
    $size = RegExData("\d*",$match,0)

    $expression = quote("sha")+"\s*:\s*"+quote("[0-9a-f]+")
    $match = RegExData($expression,$metadata,0)
    $fingerprint = RegExData("[0-9a-f]+",$match,0)

    $data = GetUrl($BannerDataUrlUrl)
    IF Equal(Left($data,7),"*error*")
        MsgOk msg="Cannot load banner list "+$data
        Cancel
    ENDIF

    $status = sqlite("sql","DELETE FROM bannerlist","sqlget=0")
    $status = sqlite("import",$data,"table=bannerlist delim=,")
    $status = sqlite("sql","DELETE FROM bannerlist WHERE code='code';")
    
    $status = Sqlite("sql","DELETE FROM _gen_banner_metadata WHERE key='fingerprint'")
    $status = Sqlite("sql","INSERT INTO _gen_banner_metadata (key,value) VALUES('fingerprint','$fingerprint')")

    GOSUB name=CheckForUpdates
ENDSUB



BEGINSUB name=GenerateBanner

    $big = addstr(1,"clear")

    # Fun Banner: "Ich habe das Listing gelesen/International Space Station"
    IF $cboxInterSpaceStation = True
        $html = $HtmlBannerFragmentTemplate
        $html = Replace("%LINK%","https://coord.info/GC1BE91",$html)
        $html = Replace("%IMGURL%","https://d1u1p2xjjiahg3.cloudfront.net/33cdfdfc-3386-4ebf-8c63-694bf45b5c68.gif",$html)
        $html = Replace("%STYLE%","",$html)
        $html = Replace("%ALT%","Banner for International Space Station (GC1BE91)",$html)
        $html = Replace("%TITLE%","International Space Station (GC1BE91)",$html)
        $big = addstr(1,"add",$html)
    ENDIF

    $Status = sqlite("sql",$sqlYourBanners,"sqlget=0")
    WHILE not($_sqleol)
        $gccode = sqlget("code")
        $link = ""
        IF IsEmpty($link) = True
            $link = "https://coord.info/"+$gccode
        ENDIF

        $width = sqlget("width")
        $height = sqlget("height")
        $style=""
        IF IsEmpty($width) = False 
            $style = $style + "width:"+$width+"px;"
        ENDIF
        IF IsEmpty($height) = False 
            $style = $style + "height:"+$height+"px;"
        ENDIF
        $style = $style + sqlget("style")

        $img = sqlget("imageUrl")

        $encodeName = sqlget("name")
        $encodeName = Replace("&","&amp;",$encodeName)
        $encodeName = Replace(chr(34),"&quot;",$encodeName)
        $encodeName = Replace("<","&lt;",$encodeName)
        $encodeName = Replace(">","&gt;",$encodeName)
        $encodeName = Replace("'","&apos;",$encodeName)

        $title = $encodeName+" ("+$gccode+")"
        $alt = "Banner for "+$title

        $html = $HtmlBannerFragmentTemplate
        $html = Replace("%LINK%",$link,$html)
        $html = Replace("%IMGURL%",$img,$html)
        $html = Replace("%STYLE%",$style,$html)
        $html = Replace("%ALT%",$alt,$html)
        $html = Replace("%TITLE%",$title,$html)
        $big = addstr(1,"add",$html)
 
        SQLNEXT
    ENDWHILE

    $big = addstr(1,"get")
    IF $rbtnOutputFile = True
        $r = PutFile($editFolderSelect+"\banner.html",$big)
        # TODO error check 
        # TODO feedback to user
    ENDIF
    IF $rbtnOutputClipboard = True
        CLIP Data=$big
        # TODO feddback to user 
    ENDIF

ENDSUB

<data> VarName=$sqlYourBanners
SELECT * FROM Caches
    INNER JOIN bannerlist
    ON Caches.Code = bannerlist.code
    WHERE Caches.Found >= 1
    ORDER BY FoundByMeDate ASC
<enddata>

# import data into the database
<data> VarName=$sqlTableBannerList
CREATE TABLE IF NOT EXISTS bannerlist (
    code varchar(12),
    imageUrl varchar(255),
    linkUrl varchar(255),
    width int,
    height int,
    style varchar(255)
);
<enddata>

<data> VarName=$sqlTableBannerMetadata
CREATE TABLE IF NOT EXISTS _gen_banner_metadata (
    key varchar(255) UNIQUE,
    value varchar(255),
    timestamp DATETIME DEFAULT CURRENT_TIMESTAMP
);
<enddata>

<data> VarName=$HtmlBannerFragmentTemplate
<p style="text-align:center;">
  <a href="%LINK%" target="_blank">
    <img src="%IMGURL%" style="%STYLE%" title="%TITLE%" alt="%ALT%"/>
  </a>
</p>
<enddata>

<data> VarName=$formMain
Name = Form1
  Type = Form
  Height = 377
  Width = 500

Name = boxOutput
  Type = Groupbox
  Caption = Output
  Height = 97
  Left = 8
  Top = 208
  Width = 473
  Taborder = 4

Name = boxOptions
  Type = Groupbox
  Caption = Options
  Height = 65
  Left = 8
  Top = 128
  Width = 473
  Taborder = 5

Name = boxBannerData
  Type = Groupbox
  Caption = Banner data
  Height = 105
  Left = 8
  Top = 8
  Width = 473
  Taborder = 6

Name = labelBannerListStatus
  Type = Label
  Container = boxBannerData
  Height = 17
  Left = 56
  Top = 24
  Width = 52
  Caption = unknown

Name = btnClose
  Type = Button
  Height = 25
  Left = 264
  Top = 312
  Width = 107
  Taborder = 2
  Caption = Close

Name = editFolderSelect
  Type = Folder
  Container = boxOutput
  Height = 21
  Left = 24
  Top = 64
  Width = 433
  Taborder = 0

Name = btnGenerate
  Type = Button
  Height = 25
  Left = 136
  Top = 312
  Width = 105
  Taborder = 3
  Caption = Generate

Name = rbtnOutputFile
  Type = Radiobutton
  Container = boxOutput
  Height = 20
  Left = 8
  Top = 40
  Width = 105
  Taborder = 1
  Caption = Filesystem

Name = rbtnOutputClipboard
  Type = Radiobutton
  Container = boxOutput
  Height = 20
  Left = 8
  Top = 16
  Width = 98
  Taborder = 2
  Caption = Clipboard

Name = cboxInterSpaceStation
  Type = Checkbox
  Container = boxOptions
  Height = 20
  Left = 8
  Top = 24
  Width = 325
  Taborder = 0
  Caption = Fun Banner for the International Space Station

Name = btnDownload
  Type = Button
  Container = boxBannerData
  Height = 25
  Left = 352
  Top = 16
  Width = 105
  Taborder = 0
  Caption = Download

Name = Label1
  Type = Label
  Container = boxBannerData
  Height = 17
  Left = 24
  Top = 48
  Width = 82
  Caption = Download Date

Name = txtDownloadDate
  Type = Label
  Container = boxBannerData
  Height = 17
  Left = 128
  Top = 48
  Width = 25
  Caption = n/a

Name = Label3
  Type = Label
  Container = boxBannerData
  Height = 17
  Left = 16
  Top = 24
  Width = 41
  Caption = Status:

Name = Label4
  Type = Label
  Container = boxBannerData
  Height = 17
  Left = 24
  Top = 72
  Width = 64
  Caption = # of Banner

Name = txtNumBanner
  Type = Label
  Container = boxBannerData
  Height = 17
  Left = 128
  Top = 72
  Width = 25
  Caption = n/a

<enddata>
