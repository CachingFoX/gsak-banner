#*******************************************
# MacVersion = 0.1
# MacDescription = Create banners for your founds
# MacAuthor = CachingFoX
# MacFileName = banner.gsk
# MacUrl = https://raw.githubusercontent.com/CachingFoX/gsak-banner/master/banner.gsk
#*******************************************

<data> VarName=$HtmlBannerFragmentTemplate
<p style="text-align:center;">
  <a href="%LINK%" target="_blank">
    <img src="%IMGURL%" style="%STYLE%" title="%TITLE%" alt="%ALT%"/>
  </a>
</p>
<enddata>

$OutputFile = "Z:\gsak-banner\banner.html" # $_CurrentDataPath + "\banners.html"

$BannerDataUrlUrl = "https://raw.githubusercontent.com/CachingFoX/gsak-banner/master/data/banners.csv"
$BannerListFile = $_AppData + "\banner-list.csv"
$BannerListFingerprintUrl = "https://api.github.com/repos/CachingFoX/gsak-banner/contents/data"
$BannerListFingerprintFile = $_AppData + "\banner-list.fingerprint"

$metadata = GetUrl($BannerListFingerprintUrl)
IF Equal(Left($metadata,7),"*error*")
  MsgOk msg="Cannot load metainfo "+$metadata
  Cancel
ELSE
  $expression = quote("size")+"\s*:\s*\d*"
  $match = RegExData($expression,$metadata,0)
  $size = RegExData("\d*",$match,0)

  $expression = quote("sha")+"\s*:\s*"+quote("[0-9a-f]+")
  $match = RegExData($expression,$metadata,0)
  $fingerprint = RegExData("[0-9a-f]+",$match,0)

  $current_fingerprint = GetFile($BannerListFingerprintFile)
  IF Left($current_fingerprint,7) = "*Error*" OR $current_fingerprint <> $fingerprint
    IF YesNo("Newer banner list is available. Do you want to download it? (size: "+$size+" bytes)" )
      $data = GetUrl($BannerDataUrlUrl)
      IF Equal(Left($data,7),"*error*")
        MsgOk msg=$data
        CANCEL
      ELSE
        $status = PutFile($BannerListFile,$data)
        IF Equal(Left($data,7),"*error*")
          MsgOk msg=$status
          CANCEL
        ELSE
          $status = PutFile($BannerListFingerprintFile,$fingerprint)
        ENDIF
      ENDIF
    ELSE
    ENDIF
  ENDIF
ENDIF

$data = GetFile($BannerListFile)
IF Equal(Left($data,7),"*error*")
  MsgOk msg=$status
  CANCEL
ENDIF

FILEERASE File=$OutputFile OnError=Continue
$rows = Val(CsvGet($data,"rows"))
$x = 1
REPEAT
  $x = $x + 1
  $gccode = CsvGet("*","$x,1")
  $img = CsvGet("*","$x,2")
  $link = CsvGet("*","$x,3")

  IF IsEmpty($link) = True
    $link = "https://coord.info/"+$gccode
  ENDIF
  $width = CsvGet("*","$x,4")
  $height = CsvGet("*","$x,5")
  $style=""
  IF IsEmpty($width) = False 
    $style = $style + "width:"+$width+"px;"
  ENDIF
  IF IsEmpty($height) = False 
    $style = $style + "height:"+$height+"px;"
  ENDIF
  $style = $style + CsvGet("*","$x,5")

  $html = $HtmlBannerFragmentTemplate
  $html = Replace("%LINK%",$link,$html)
  $html = Replace("%IMGURL%",$img,$html)
  $html = Replace("%STYLE%",$style,$html)
  $html = Replace("%ALT%",$gccode,$html)
  $html = Replace("%TITLE%",$gccode,$html)
  $r = AppendFile($OutputFile,$html)
UNTIL $x = $rows
